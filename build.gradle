import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

var applyPlugins = [
    'eclipse', 'idea', 'maven-publish',
    'net.minecraftforge.gradle', 'org.parchmentmc.librarian.forgegradle',
    'org.spongepowered.mixin'
]
applyPlugins.forEach(pl -> apply plugin: pl)

apply from: 'https://raw.githubusercontent.com/sciwhiz12/Concord/dev/1.18.x/gradle/version.gradle'

def mcVersion = properties.get('mc_version')
def modid = properties.get('mod_id')
version = properties.get('version')
group = 'com.matyrobbrt.sectionprotection' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "$modid-$mcVersion"

sourceSets {
    api {}
}

configurations {
    library
    implementation.extendsFrom(library)

    apiImplementation.extendsFrom(minecraft)
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
    config "${modid}.mixins.json"
    
    debug.verbose = true
    debug.export = true
}

println "Project Version: $version"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.05.02-1.18.2'

    runs {
        configureEach {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'sectionprotection'
            arg "-mixin.config=${modid}.mixins.json"

            mods {
                sectionprotection {
                    source sourceSets.api
                    source sourceSets.main
                }
            }

            lazyToken('minecraft_classpath') {
                configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
            }
        }

        client {
            workingDirectory project.file('run')
            args '--uuid', '5349e1faf20a47cfbd48b7acd2ac8dba'
        }

        server {
            workingDirectory project.file('run')
        }

        gameTestServer {
            workingDirectory project.file('run')
        }

        data {
            workingDirectory project.file('run')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://cursemaven.com' }
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:$mcVersion-$forge_version"

    implementation sourceSets.api.output

    runtimeOnly fg.deobf("curse.maven:jei-238222:3745255")
    runtimeOnly fg.deobf("curse.maven:flywheel-486392:3737402")
    runtimeOnly fg.deobf("curse.maven:create-328085:3737418")

    compileOnly fg.deobf("curse.maven:journeymap-32274:3796379")

    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

    library('io.github.matyrobbrt:asmutils:0.3.1') {
        exclude module: 'asm'
        exclude module: 'asm-tree'
        exclude module: 'asm-util'
        exclude module: 'asm-commons'
        exclude module: 'slf4j-api'
    }
    library('io.github.matyrobbrt:eventdispatcher:1.6.0') {
        // exclude module: 'asmutils'
        exclude module: 'asm'
        exclude module: 'asm-tree'
        exclude module: 'asm-commons'
        exclude module: 'slf4j-api'
        exclude module: 'typetools'
    }

    compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
}

final var expands = [
    'version': project.version,
    'modid': modid
]

processResources {
    filesMatching('META-INF/mods.toml') {
        expand expands
    }
    exclude ".cache/cache"
}

final var actualDateTime = OffsetDateTime.now(ZoneOffset.UTC).withNano(0)
final var currentDateTime = DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(actualDateTime)
def manifestAttributes = [
    "Specification-Title"     : "SectionProtection",
    "Specification-Vendor"    : "Matyrobbrt",
    "Specification-Version"   : "1",
    "Implementation-Title"    : project.name,
    "Implementation-Version"  : project.jar.archiveVersion,
    "Implementation-Vendor"   : "Matyrobbrt",
    "Timestamp"               : currentDateTime,
    "Git-Commit"              : versions.getCommitId(),
    "Git-Commit-Timestamp"    : versions.getCommitTimestamp(),
    "MixinConfigs"            : "${modid}.mixins.json"
] as Map

jar {
    from sourceSets.api.output
    archiveClassifier.set('slim')
    manifest.attributes(manifestAttributes);
}

shadowJar {
    from sourceSets.api.output
    archiveClassifier.set('')
    configurations = [project.configurations.library]
    relocate 'io.github.matyrobbrt.asmutils', "${project.group}.shade.asmutils"
    relocate 'io.github.matyrobbrt.eventdispatcher', "${project.group}.shade.eventdispatcher"
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
