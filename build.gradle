import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

var applyPlugins = [
    'eclipse', 'idea', 'maven-publish',
    'net.minecraftforge.gradle', 'org.parchmentmc.librarian.forgegradle',
    'org.spongepowered.mixin'
]
applyPlugins.forEach(pl -> apply plugin: pl)

apply from: 'https://raw.githubusercontent.com/sciwhiz12/Concord/dev/1.18.x/gradle/version.gradle'

def mcVersion = properties.get('mc_version')
def modid = properties.get('mod_id')
version = properties.get('version')
group = 'com.matyrobbrt.sectionprotection' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "$modid-$mcVersion"

mixin {
    add sourceSets.main, "${modid}.refmap.json"
    config "${modid}.mixins.json"
    
    debug.verbose = true
    debug.export = true
}

println "Project Version: $version"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.05.02-1.18.2'

    runs {
        all {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'sectionprotection'
            arg "-mixin.config=${modid}.mixins.json"

            mods {
                sectionprotection {
                    source sourceSets.main
                }
            }
        }

        client {
            workingDirectory project.file('run')
        }

        server {
            workingDirectory project.file('run')
        }

        gameTestServer {
            workingDirectory project.file('run')
        }

        data {
            workingDirectory project.file('run')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://cursemaven.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mcVersion-$forge_version"

    runtimeOnly fg.deobf("curse.maven:jei-238222:3745255")
    runtimeOnly fg.deobf("curse.maven:flywheel-486392:3737402")
    runtimeOnly fg.deobf("curse.maven:create-328085:3737418")

    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

final var expands = [
    'version': project.version,
    'modid': modid
]

processResources {
    filesMatching('META-INF/mods.toml') {
        expand expands
    }
}

final var actualDateTime = OffsetDateTime.now(ZoneOffset.UTC).withNano(0)
final var currentDateTime = DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(actualDateTime)
def manifestAttributes = [
    "Specification-Title"     : "SectionProtection",
    "Specification-Vendor"    : "Matyrobbrt",
    "Specification-Version"   : "1",
    "Implementation-Title"    : project.name,
    "Implementation-Version"  : project.jar.archiveVersion,
    "Implementation-Vendor"   : "Matyrobbrt",
    "Timestamp"               : currentDateTime,
    "Git-Commit"              : versions.getCommitId(),
    "Git-Commit-Timestamp"    : versions.getCommitTimestamp(),
    "MixinConfigs"            : "${modid}.mixins.json"
] as Map

jar {
    manifest.attributes(manifestAttributes)
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
